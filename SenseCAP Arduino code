#include <SoftwareSerial.h>
#include <Arduino.h>
#include <SensirionI2CSht4x.h>   //SHT40 library
#include <Wire.h>
#include "TFT_eSPI.h"  //TFT LCD library



// Soil Moisture Sensor
int sensorPin = A1;
int sensorValue = 100;



TFT_eSPI tft;  //Initializing TFT LCD
TFT_eSprite spr = TFT_eSprite(&tft);  //Initializing buffer



SoftwareSerial mySerial(A0, A1); // RX, TX



SensirionI2CSht4x sht4x;



static char recv_buf[512];
static bool is_exist = false;
static bool is_join = false;



static int at_send_check_response(char *p_ack, int timeout_ms, char *p_cmd, ...)
{
  int ch;
  int num = 0;
  int index = 0;
  int startMillis = 0;
  va_list args;
  memset(recv_buf, 0, sizeof(recv_buf));
  va_start(args, p_cmd);
  mySerial.printf(p_cmd, args);
  Serial.printf(p_cmd, args);
  va_end(args);
  delay(200);
  startMillis = millis();



 if (p_ack == NULL)
  {
    return 0;
  }



 do
  {
    while (mySerial.available() > 0)
    {
      ch = mySerial.read();
      recv_buf[index++] = ch;
      Serial.print((char)ch);
      delay(2);
    }



   if (strstr(recv_buf, p_ack) != NULL)
    {
      return 1;
    }



 } while (millis() - startMillis < timeout_ms);
  return 0;
}



static void recv_prase(char *p_msg)
{
  if (p_msg == NULL)
  {
    return;
  }
  char *p_start = NULL;
  int data = 0;
  int rssi = 0;
  int snr = 0;



 p_start = strstr(p_msg, "RX");
  if (p_start && (1 == sscanf(p_start, "RX: \"%d\"\r\n", &data)))
  {
    Serial.println(data);
  }



 p_start = strstr(p_msg, "RSSI");
  if (p_start && (1 == sscanf(p_start, "RSSI %d,", &rssi)))
  {
    Serial.println(rssi);
  }



 p_start = strstr(p_msg, "SNR");
  if (p_start && (1 == sscanf(p_start, "SNR %d", &snr)))
  {
    Serial.println(snr);
  }
}



void setup(void)
{
  Serial.begin(115200);
  mySerial.begin(9600);



 Wire.begin();



 tft.begin();  //Start TFT LCD
  tft.setRotation(3);  //Set TFT LCD rotation
  spr.createSprite(TFT_HEIGHT, TFT_WIDTH); //Create buffer



 pinMode(WIO_LIGHT, INPUT);  //Set light sensor pin as INPUT
  pinMode(WIO_BUZZER, OUTPUT);  //Set Buzzer sensor pin as OUTPUT



 uint16_t error;
  char errorMessage[256];



 sht4x.begin(Wire);



 uint32_t serialNumber;
  error = sht4x.serialNumber(serialNumber);
  delay(5000);
  if (error) {
    Serial.print("Error trying to execute serialNumber(): ");
    errorToString(error, errorMessage, 256);
    Serial.println(errorMessage);
  } else {
    Serial.print("Serial Number: ");
    Serial.println(serialNumber);
  }



 Serial.print("E5 LORAWAN TEST\r\n");



 if (at_send_check_response("+AT: OK", 100, "AT\r\n"))
  {
    is_exist = true;
    at_send_check_response("+ID: DevEui", 1000, "AT+ID=DevEui,\"xxxxxxxxxxxxxxxx\"\r\n");
    at_send_check_response("+ID: AppEui", 1000, "AT+ID=AppEui,\"xxxxxxxxxxxxxxxx\"\r\n");
    at_send_check_response("+MODE: LWOTAA", 1000, "AT+MODE=LWOTAA\r\n");
    at_send_check_response("+DR: AS923", 1000, "AT+DR=AS923\r\n");
    at_send_check_response("+CH: NUM", 1000, "AT+CH=NUM,0-2\r\n");
    at_send_check_response("+KEY: APPKEY", 1000, "AT+KEY=APPKEY,\"xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx\"\r\n");
    at_send_check_response("+CLASS: A", 1000, "AT+CLASS=A\r\n");
    at_send_check_response("+PORT: 8", 1000, "AT+PORT=8\r\n");
    delay(200);
    is_join = true;
  }
  else
  {
    is_exist = false;
    Serial.print("No E5 module found.\r\n");
  }
}
void loop(void) {
  uint16_t error;
  float temperature, humidity;
  int int_temp, int_humi;
  error = sht4x.measureHighPrecision(temperature, humidity);
  int_temp = temperature * 100;
  int_humi = humidity * 100;
  int light = analogRead(WIO_LIGHT);   //Assign variable to store light sensor values
  int mic = (analogRead(WIO_MIC))/10;   //Assign variable to store microphone sensor values



 //Setting the title header
  spr.fillSprite(TFT_WHITE);  //Fill background with white color
  spr.fillRect(0, 0, 320, 50, TFT_DARKGREEN); //Rectangle fill with dark green
  spr.setTextColor(TFT_WHITE);  //Setting text color
  spr.setTextSize(4);  //Setting text size
  spr.drawString("Agrovoltaic", 30, 15); //Drawing a text string
  spr.drawFastVLine(150, 50, 190, TFT_DARKGREEN); //Drawing verticle line
  spr.drawFastHLine(0, 140, 320, TFT_DARKGREEN); //Drawing horizontal line



 //Setting Temperature
  spr.setTextColor(TFT_BLACK);
  spr.setTextSize(2);
  spr.drawString("Temperature", 10, 65);
  spr.setTextSize(3);
  spr.drawNumber(temperature, 50, 95); //Display Temperature values
  spr.drawString("C", 90, 95);



 //Setting Humidity
  spr.setTextSize(2);
  spr.drawString("Humidity", 25, 160);
  spr.setTextSize(3);
  spr.drawNumber(humidity, 30, 190); //Display Humidity values
  spr.drawString("%RH", 70, 190);



 //Setting Soil Moisture
  sensorValue = analogRead(sensorPin); //store sensor values
  spr.drawString("Soil Moisture", 160, 65);
  spr.setTextSize(3);
  spr.drawNumber(sensorValue, 200, 95); // display sensor values as percentage

  //Setting light
  spr.setTextSize(2);
  spr.drawString("Light", 200, 160);
  spr.setTextSize(3);
  light = map(light, 0, 1023, 0, 100); //Map sensor values
  spr.drawNumber(light, 205, 190); //Display sensor values as percentage
  spr.drawString("%", 245, 190);
  spr.pushSprite(0, 0); //Push to LCD

  if (is_exist) {
    int ret = 0;
    if (is_join) {
      ret = at_send_check_response("+JOIN: Network joined", 12000, "AT+JOIN\r\n");
      if (ret) {
        is_join = false;
      }
      else {
        Serial.println("");
        Serial.print("JOIN failed!\r\n\r\n");
        delay(5000);
      }
    }
    else {
      char cmd[128];
      sprintf(cmd, "AT+CMSGHEX=\"%04X %04X %04X %04X\"\r\n", int_temp, int_humi, light, mic);
      ret = at_send_check_response("Done", 10000, cmd);
      if (ret) {
        Serial.print("Temperature: ");  Serial.print(temperature);
        Serial.println();
        Serial.print("Humidity: ");  Serial.println(humidity);
        Serial.print("Light: "); Serial.println(light);
        Serial.print("Microphone: "); Serial.println(mic);
        recv_prase(recv_buf);
      }
      else {
        Serial.print("Send failed!\r\n\r\n");
      }
      delay(5000);
    }
  }
  else
  {
    delay(1000);
  }
}
